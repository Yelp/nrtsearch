plugins {
    id 'com.diffplug.spotless' version '6.22.0'
    id 'distribution'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    id 'java'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

group 'com.yelp.nrtsearch.plugins'
version '0.0.1'

repositories {
    mavenCentral()
}

ext {
    assertjVersion = '3.19.0'
    grpcVersion = "1.46.0"
    junitVersion = "4.13.2"
    nrtsearchVersion = "0.+"
    protobufVersion = "3.24.3"
}

dependencies {
    compileOnly "com.yelp.nrtsearch:server:${nrtsearchVersion}"
    testImplementation "com.yelp.nrtsearch:server:${nrtsearchVersion}"
    testImplementation("com.yelp.nrtsearch:server:${nrtsearchVersion}:tests")
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "com.google.protobuf:protobuf-java:${protobufVersion}"
}

distributions {
    main {
        contents {
            from(jar)
            from(this.project.configurations.runtimeClasspath)
            from('src/main/plugin-metadata/plugin-metadata.yaml') {
                expand(
                        classname: 'com.yelp.nrtsearch.plugins.example.ExamplePlugin',
                        name: "nrtsearch-plugin-example",
                        version: project.version,
                        description: 'Example plugin for nrtsearch',
                        server_version: "${nrtsearchVersion}"
                )
            }
        }
    }
}

test {
    finalizedBy 'spotlessJavaCheck'
}

spotless {
    java {
        licenseHeaderFile '../license_header'
        removeUnusedImports()
        endWithNewline()
        trimTrailingWhitespace()
        googleJavaFormat()
    }
}
