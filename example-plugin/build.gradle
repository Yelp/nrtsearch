plugins {
    alias(libs.plugins.spotless)
    id 'distribution'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    id 'java'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

group 'com.yelp.nrtsearch.plugins'
version '0.0.1'

ext {
    nrtsearchVersion = "1.+"
}

dependencies {
    compileOnly "com.yelp.nrtsearch:server:${nrtsearchVersion}"
    testImplementation "com.yelp.nrtsearch:server:${nrtsearchVersion}"
    testImplementation("com.yelp.nrtsearch:server:${nrtsearchVersion}:tests")
    testImplementation libs.assertj.core
    testImplementation libs.grpc.testing
    testImplementation libs.junit
    testImplementation libs.protobuf.java
    testImplementation("org.awaitility:awaitility:4.3.0")
}

distributions {
    main {
        contents {
            from(jar)
            from(this.project.configurations.runtimeClasspath)
            from('src/main/plugin-metadata/plugin-metadata.yaml') {
                expand(
                        classname: 'com.yelp.nrtsearch.plugins.example.ExamplePlugin',
                        name: "nrtsearch-plugin-example",
                        version: project.version,
                        description: 'Example plugin for nrtsearch',
                        server_version: "${nrtsearchVersion}"
                )
            }
        }
    }
}

test {
    finalizedBy 'spotlessJavaCheck'
}

spotless {
    java {
        licenseHeaderFile '../license_header'
        removeUnusedImports()
        endWithNewline()
        trimTrailingWhitespace()
        googleJavaFormat()
    }
}
