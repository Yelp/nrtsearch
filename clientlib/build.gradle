plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.8'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    // Publish clientlib to maven central
    id 'maven-publish'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

sourceCompatibility = 1.12
targetCompatibility = 1.12
startScripts.enabled = false

def grpcVersion = '1.26.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.11.1'
def protocVersion = protobufVersion

dependencies {
    //grpc deps
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "javax.annotation:javax.annotation-api:1.2"

    // examples/advanced need this for JsonFormat
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    //test deps
    compile "io.grpc:grpc-testing:${grpcVersion}"
}

sourceSets {
    all {
        proto {
            srcDirs 'proto'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.yelp.nrtsearch'
            artifactId = 'clientlib'
            version = '0.1'
        }
    }
}

test {
    // Add tests when there's java code in this module
    // Until then, exclude everything to allow
    // build to run
    exclude '**/*'
}